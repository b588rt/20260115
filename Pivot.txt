

Sub CreatePivotTable()

    Dim wsData As Worksheet
    Dim wsPivot As Worksheet
    Dim pc As PivotCache
    Dim pt As PivotTable
    Dim lastRow As Long
    Dim lastCol As Long
    Dim dataRange As Range
    Dim fieldPartNo As String
    Dim fieldBatch As String
    Dim findPartNo As Range
    Dim findBatch As Range

    On Error Resume Next
    Set wsData = ThisWorkbook.Worksheets("工作表1")
    On Error GoTo 0
    
    If wsData Is Nothing Then

        Exit Sub
    End If

    Set findPartNo = wsData.Range("A1:Z1").Find(What:="教室", LookIn:=xlValues, LookAt:=xlWhole)
    Set findBatch = wsData.Range("A1:Z1").Find(What:="學號", LookIn:=xlValues, LookAt:=xlWhole)

    If findPartNo Is Nothing Or findBatch Is Nothing Then

        Exit Sub
    End If

    fieldPartNo = findPartNo.Value
    fieldBatch = findBatch.Value


    lastRow = wsData.Cells(wsData.Rows.Count, findPartNo.Column).End(xlUp).Row
    lastCol = wsData.Cells(1, wsData.Columns.Count).End(xlToLeft).Column
    Set dataRange = wsData.Range(wsData.Cells(1, 1), wsData.Cells(lastRow, lastCol))


    On Error Resume Next
    Application.DisplayAlerts = False
    Sheets("樞紐分析結果").Delete
    Application.DisplayAlerts = True
    On Error GoTo 0
    Set wsPivot = Sheets.Add
    wsPivot.Name = "樞紐分析結果"


    Set pc = ThisWorkbook.PivotCaches.Create( _
        SourceType:=xlDatabase, _
        SourceData:=dataRange)

    Set pt = pc.CreatePivotTable( _
        TableDestination:=wsPivot.Range("A3"), _
        TableName:="PartNoPivotTable")


    With pt
        ' 將「教室」加入「列」
        With .PivotFields(fieldPartNo)
            .Orientation = xlRowField
            .Position = 1
        End With


        .AddDataField .PivotFields(fieldBatch), "計數 - 學號", xlCount
    End With


    wsPivot.Columns("A:B").AutoFit

End Sub

